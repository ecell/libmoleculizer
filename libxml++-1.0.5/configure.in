AC_INIT(libxml++/libxml++.h)

#release versioning
GENERIC_MAJOR_VERSION=1
GENERIC_MINOR_VERSION=0
GENERIC_MICRO_VERSION=5
GENERIC_VERSION=$GENERIC_MAJOR_VERSION.$GENERIC_MINOR_VERSION.$GENERIC_MICRO_VERSION
AC_SUBST(GENERIC_MAJOR_VERSION)
AC_SUBST(GENERIC_MINOR_VERSION)
AC_SUBST(GENERIC_VERSION)

#shared library versioning
GENERIC_LIBRARY_VERSION=1:4:0
#                       | | |
#                +------+ | +---+
#                |        |     |
#             current:revision:age
#                |        |     |
#                |        |     +- increment if interfaces have been added
#                |        |        set to zero if interfaces have been removed
#                                  or changed
#                |        +- increment if source code has changed
#                |           set to zero if current is incremented
#                +- increment if interfaces have been added, removed or changed
AC_SUBST(GENERIC_LIBRARY_VERSION)



VERSION=$GENERIC_VERSION


AM_INIT_AUTOMAKE(libxml++, $GENERIC_VERSION)

AC_PROG_CXX
AC_PROG_CXXCPP
AC_LANG_CPLUSPLUS
AC_PROG_INSTALL

AC_CANONICAL_HOST

AC_MSG_CHECKING([for Win32 Platform])

case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    WIN32_LDFLAGS=-no-undefined
    WIN32_LIBADD=-lstdc++
    ;;
  *)
    platform_win32=no
    WIN32_LDFLAGS=
    WIN32_LIBADD=
    ;;
esac

AC_SUBST(WIN32_LDFLAGS)
AC_SUBST(WIN32_LIBADD)
AC_MSG_RESULT([$platform_win32])

AC_LIBTOOL_WIN32_DLL

AM_PROG_LIBTOOL

AC_CHECK_HEADERS(string list map, , exit)

# Dummy conditional just to make automake-1.4 happy.
# We need an always-false condition in docs/Makefile.am.
AM_CONDITIONAL(LIBXMLCPP_FALSE,[false])

AC_CONFIG_SUBDIRS([libxml2])

AC_OUTPUT(
  Makefile
  libxml++/Makefile
    libxml++/parsers/Makefile
    libxml++/exceptions/Makefile
    libxml++/nodes/Makefile
    libxml++/io/Makefile

  libxml++-1.0.pc
  libxml++.spec
)
